plugins {
    id "com.android.application"
    id "kotlin-android"
}

ext {
    useGoogleGcm = true
}

android {

    defaultConfig {
        applicationId "be.casperverswijvelt.unifiedinternetqs"
        targetSdkVersion 35
        compileSdk 35
        minSdkVersion 31
        versionCode 3010001
        versionName "3.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            ext.enableCrashlytics = false
        }
    }
    flavorDimensions = ["distribution"]
    productFlavors {
        playstore {
            dimension "distribution"
            versionNameSuffix "-playstore"
            ext.useGoogleGcm = true
        }
        github {
            dimension "distribution"
            versionNameSuffix "-github"
            ext.useGoogleGcm = true
        }
        fdroid {
            dimension "distribution"
            versionNameSuffix "-fdroid"
            ext.useGoogleGcm = false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    namespace 'be.casperverswijvelt.unifiedinternetqs'

    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.11"
    }
    androidResources {
        generateLocaleConfig = true
    }
}
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.preference:preference-ktx:1.2.1"

    // Compose UI
    implementation 'androidx.activity:activity-compose:1.9.2'
    implementation 'androidx.compose.animation:animation:1.7.3'
    implementation 'androidx.compose.ui:ui-tooling:1.7.3'
    implementation "androidx.compose.material3:material3:1.3.0"
    implementation "androidx.navigation:navigation-compose:2.8.2"
    implementation "androidx.compose.material:material-icons-extended:1.7.3"

    // App restarting
    implementation "com.jakewharton:process-phoenix:2.1.2"

    // Preferences DataStore
    implementation "androidx.datastore:datastore-preferences:1.1.1"

    // SU
    implementation "com.github.topjohnwu.libsu:core:5.0.3"

    // Shizuku
    def shizuku_version = "12.1.0"
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"

    // Firebase Analytics
    githubImplementation "com.google.firebase:firebase-crashlytics-ktx:19.2.0"
    playstoreImplementation "com.google.firebase:firebase-crashlytics-ktx:19.2.0"
}

configurations {
    configureEach {
        exclude group: "androidx.lifecycle", module: "lifecycle-viewmodel-ktx"
    }
}

android.productFlavors.each { flavor ->
    {
        if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains(flavor.name) && flavor.ext.useGoogleGcm) {
            println("Building flavor with Google GCM [${flavor.name}] - applying plugin")
            apply plugin: "com.google.gms.google-services"
            apply plugin: "com.google.firebase.crashlytics"
        }
    }
}
